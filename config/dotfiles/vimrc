" Better display for messages
set cmdheight=2

" better file related searches
set path+=**

command! MakeTags !ctags -R .

" always show signcolumns
set signcolumn=yes

" show ruler
set colorcolumn=81

" set updatetime=100

set encoding=utf-8

" turn on line numbers
set number relativenumber
set showcmd

" allow unsaved buffers to be hidden (ie not displayed)
set hidden

set autoindent
filetype plugin indent on

"By default use a tab width of 4 space chars.
set tabstop=4       " The width of a TAB is set to 4.
                    " Still it is a \t. It is just that
                    " Vim will interpret it to be having
                    " a width of 4.
set shiftwidth=4    " Indents will have a width of 4.
set softtabstop=4   " Sets the number of columns for a TAB.
set expandtab       " Expand TABs to spaces.

if has("autocmd")
    autocmd BufNewFile,BufRead *.ts,*.js,*.tsx,*.jsx set filetype=typescript
    " Use actual tab chars in Makefiles.
    autocmd FileType make set tabstop=8 shiftwidth=8 softtabstop=0 noexpandtab
    autocmd FileType yaml setlocal ts=2 sts=2 sw=2 expandtab
    autocmd FileType markdown setlocal ts=2 sts=2 sw=2 expandtab
    autocmd FileType sh setlocal ts=2 sts=2 sw=2 expandtab
    autocmd FileType typescript setlocal ts=2 sts=2 sw=2 expandtab
   
    " highlight during search but not after
    augroup vimrc-incsearch-highlight
      autocmd!
      autocmd CmdLineEnter /,\? :set hlsearch
      autocmd CmdlineLeave /,\? :set nohlsearch
    augroup END
endif

set wrap
set linebreak
set breakindent

set undofile
set undodir=~/.vim/undodir

set ignorecase
set smartcase

set splitbelow
set splitright

set completeopt=menuone

set noshowmode

set nospell

" fix backspace on mac
set backspace=indent,eol,start

set foldmethod=indent

" enable mouse in normal mode and use multiple clicks to select word, line etc
set mouse=nv

" opens search results in a window w/ links and highlight the matches
command! -nargs=+ Grep execute 'silent grep! -I -r -n --exclude=\*.{json,pyc} -e <args>' | copen | execute 'silent /<args>'
" shift-control-* Greps for the word under the cursor
" :nmap <leader>g :Grep <c-r>=expand("<cword>")<cr><cr>
" nmap <c-f> :Grep 

" necessary mappings
nnoremap Y y$

nnoremap <c-e> 3<c-e>
nnoremap <c-y> 3<c-y>

" because C-a is tmux
nnoremap <a-a> <c-a>
nnoremap <a-x> <c-x>

nnoremap <A-O> O<Esc>j
nnoremap <A-o> o<Esc>k

" remap * and # to highlight and only jump if the current word is the search
" word and highlight is on
function! StarJump(key)
    if '\<' .. expand('<cword>') .. '\>' == getreg('/') && (&hlsearch)
        if a:key == "#"
            let v:searchforward=0
        endif
        normal n
    else
        call setreg('/', '\<' .. expand('<cword>') .. '\>') 
    endif
    set hlsearch
endfunction
nmap <silent> * :call StarJump("*")<CR>
nmap <silent> # :call StarJump("#")<CR>:let v:searchforward=0<CR>

" turn off hlsearch when pressing esc
nmap <silent> <ESC> :set nohlsearch<CR>

" toggle hlsearch
nnoremap <leader>h :set hlsearch!<cr>

" keep visual selection after indenting
vmap > >gv
vmap < <gv

" quickly execute q macro
nnoremap Q @q

" faster copy past and save and exit
nnoremap <Leader>Y "+Y
vnoremap <Leader>Y "+Y
nnoremap <Leader>P O<Esc>"+P
vnoremap <Leader>P O<Esc>"+P
nnoremap <Leader>y "+y
vnoremap <Leader>y "+y
nnoremap <Leader>p o<Esc>"+p
vnoremap <Leader>p o<Esc>"+p
nnoremap <Leader>q :q<CR>
nnoremap <Leader>Q :qa<CR>
nnoremap <Leader>w :w<CR>
nnoremap <Leader>W :wa<CR>

" cycle through buffers easily like tabs
nnoremap gB :bn<CR>
nnoremap gb :bp<CR>

" open help in new tab
" command! TABHELP tab help
cabbrev th tab help

source ~/.vim/vimrc-source/visual-at.vim

" store yanks in numbered registers too
function! SaveLastReg()
    if v:event['regname']==""
        if v:event['operator']=='y'
            for i in range(8,1,-1)
                exe "let @".string(i+1)." = @". string(i) 
            endfor
            if exists("g:last_yank")
                let @1=g:last_yank
            endif
            let g:last_yank=@"
        endif 
    endif
endfunction 

:autocmd TextYankPost * call SaveLastReg()

call plug#begin()

    Plug 'iamcco/markdown-preview.nvim'

    Plug 'plasticboy/vim-markdown'

    Plug 'KeitaNakamura/tex-conceal.vim', {'for': 'tex'} 
    let g:tex_superscripts= "[0-9a-zA-W.,:;+-<>/()=]"
    let g:tex_subscripts= "[0-9aehijklmnoprstuvxy,+-/().]"

    Plug 'lervag/vimtex'
    let g:tex_flavor='latex'
    let g:vimtex_quickfix_latexlog = {'default' : 0}
    let g:vimtex_quickfix_mode = 2
    let g:tex_conceal='abdmg'
    " let g:vimtex_view_general_viewer = 'zathura'
    let g:vimtex_view_method = 'zathura'
    let g:vimtex_view_general_options = '--unique file:@pdf\#src:@line@tex'
    " let g:vimtex_view_general_options_latexmk = '--unique'
    let g:vimtex_complete_enabled = 1
    " Disable custom warnings based on regexp
    let g:vimtex_quickfix_ignore_filters = [
                \ 'while executing',
                \ '\hbox',
                \ 'Marginpar'
                \]
    let g:vimtex_compiler_progname = 'nvr'
    let g:vimtex_toc_config = {
                \ 'hide_line_numbers' : 0,
                \ 'fold_enable' : 1,
                \ }

    Plug 'sirver/ultisnips'
    let g:UltiSnipsExpandTrigger = '<tab>'
    let g:UltiSnipsJumpForwardTrigger = '<tab>'
    let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'

    Plug 'michaeljsmith/vim-indent-object'

    Plug 'vim-scripts/argtextobj.vim'

    Plug 'tpope/vim-fugitive' " , { 'commit' : '9108971' }
    nnoremap <leader>G :Git 

    Plug 'tpope/vim-surround'

    Plug 'preservim/nerdtree'
    let NERDTreeShowHidden=1
    map <C-q> :NERDTreeToggle<CR>
    let NERDTreeMinimalUI = 1 " remove the ? from the top
    let NERDTreeDirArrows = 1 

    Plug 'christoomey/vim-tmux-navigator'

    Plug 'tpope/vim-commentary'
    " sometimes ctrl+/ is understood as ctrl+_
    nmap <C-_> gc
    nmap <C-/> gc
    nmap <C-_><C-_> gcc
    nmap <C-/><C-/> gcc
    vmap <C-/> gc
    vmap <C-_> gc

    Plug 'junegunn/fzf'
    Plug 'junegunn/fzf.vim'
    " redefine :Rg to also search git ignored files
    command! -bang -nargs=* Rg
                \ call fzf#vim#grep(
                \   'rg --column --line-number --no-heading --color=always --no-ignore 
                \   --smart-case -- '.shellescape(<q-args>), 1,
                \   fzf#vim#with_preview(), <bang>0)
    nmap <c-f> :Rg<CR>
    " silent! !git rev-parse --is-inside-work-tree
    " if v:shell_error == 0
    "   noremap <C-p> :GFiles --cached --others --exclude-standard<CR>
    " else
    noremap <C-p> :Files<CR>
    " endif
    noremap <leader>b :Buffers<CR>

    Plug 'JuliaEditorSupport/julia-vim'
    let g:latex_to_unicode_tab = 1
    let g:latex_to_unicode_auto = 1
    " let g:latex_to_unicode_keymap = 1

    Plug 'jpalardy/vim-slime'
    let g:slime_target = "tmux"
    let g:slime_paste_file = "/tmp/vim-slime"
    let g:slime_default_config = {"socket_name": "default", "target_pane": "%"}

    Plug 'airblade/vim-gitgutter'

    Plug 'zhimsel/vim-stay'
    set viewoptions=cursor,folds,slash,unix
    set viewoptions-=options

    Plug 'frazrepo/vim-rainbow'
    " Plug 'luochen1990/rainbow'
    let g:rainbow_active = 1

    " Plug 'mushanyoung/vim-windflower'
    " Plug 'lifepillar/vim-solarized8'
    Plug 'sainnhe/gruvbox-material'

    " Plug 'nvim-treesitter/nvim-treesitter'

    " Plug 'neovim/nvim-lsp'
    " Plug 'autozimu/LanguageClient-neovim', {'branch': 'next', 'do': 'bash install.sh'}"
    " Plug 'nvim-lua/diagnostic-nvim'                                    
    " Plug 'nvim-lua/completion-nvim'
    " source ~/.vim/vimrc-source/langclient-nvim-julia-config.vim
    Plug 'neoclide/coc.nvim', { 'branch': 'release' }
    source ~/.vim/vimrc-source/coc-vimrc-config.vim

call plug#end()

" set background=light
colorscheme gruvbox-material
" remove background highlight from comments
highlight Comment cterm=None
syntax on

" autocmd FileType julia let b:commentary_format = "## %s"

set exrc
set secure
